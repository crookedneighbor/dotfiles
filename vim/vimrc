"###########################################################################################################################
"
"  `8.`888b           ,8'  8 8888          ,8.       ,8.
"   `8.`888b         ,8'   8 8888         ,888.     ,888.
"    `8.`888b       ,8'    8 8888        .`8888.   .`8888.
"     `8.`888b     ,8'     8 8888       ,8.`8888. ,8.`8888.
"      `8.`888b   ,8'      8 8888      ,8'8.`8888,8^8.`8888.
"       `8.`888b ,8'       8 8888     ,8' `8.`8888' `8.`8888.
"        `8.`888b8'        8 8888    ,8'   `8.`88'   `8.`8888.
"         `8.`888'         8 8888   ,8'     `8.`'     `8.`8888.
"          `8.`8'          8 8888  ,8'       `8        `8.`8888.
"           `8.`           8 8888 ,8'         `         `8.`8888.
"
"###########################################################################################################################

" ========= Setup ========

set nocompatible

if &shell == "/usr/bin/sudosh"
  set shell=/bin/bash
endif

filetype off
" @TODO: Convert to Vundle
call pathogen#infect('bundle/{}', 'bundle_local/{}')
filetype plugin indent on


" ========= Options ========

compiler ruby
syntax on
set hlsearch
set number
set showmatch
set incsearch
set background=dark
set hidden
set backspace=indent,eol,start
set textwidth=0 nosmartindent tabstop=2 shiftwidth=2 softtabstop=2 expandtab
set ruler
set wrap
set dir=/tmp//
set scrolloff=5
set ignorecase
set smartcase
set wildignore+=*.pyc,*.o,*.class,*.lo,.git,vendor/*,*/node_modules/*,*/bower_components/*
set tags+=gems.tags

if version >= 703
  set undodir=~/.vim/undodir
  set undofile
  set undoreload=10000 "maximum number lines to save for undo on a buffer reload
endif
set undolevels=1000 "maximum number of changes that can be undone

" Color
colorscheme Tomorrow-Night

au FileType diff colorscheme desert
au FileType git colorscheme desert
au BufWinLeave * colorscheme Tomorrow-Night

augroup markdown
  au!
  au BufNewFile,BufRead *.md,*.markdown setlocal filetype=ghmarkdown
augroup END

" File Types

autocmd FileType php setlocal tabstop=4 shiftwidth=4 softtabstop=4
autocmd FileType python setlocal tabstop=4 shiftwidth=4 softtabstop=4
autocmd FileType java setlocal tabstop=4 shiftwidth=4 softtabstop=4
autocmd FileType cs setlocal tabstop=4 shiftwidth=4 softtabstop=4

autocmd FileType tex setlocal textwidth=78
autocmd BufNewFile,BufRead *.txt setlocal textwidth=78

autocmd FileType ruby runtime ruby_mappings.vim
autocmd FileType python runtime python_mappings.vim

if version >= 700
    autocmd BufNewFile,BufRead *.txt setlocal spell spelllang=en_us
    autocmd FileType tex setlocal spell spelllang=en_us
endif

" Highlight trailing whitespace
autocmd InsertEnter * match ExtraWhitespace /\s\+\%#\@<!$/
autocmd BufRead,InsertLeave * match ExtraWhitespace /\s\+$/

" Autoremove trailing spaces when saving the buffer
autocmd FileType c,cpp,elixir,eruby,html,jade,java,javascript,php,ruby autocmd BufWritePre <buffer> :%s/\s\+$//e

" Highlight too-long lines
autocmd BufRead,InsertEnter,InsertLeave * 2match LineLengthError /\%126v.*/
highlight LineLengthError ctermbg=black guibg=black
autocmd ColorScheme * highlight LineLengthError ctermbg=black guibg=black

" Set up highlight group & retain through colorscheme changes
highlight ExtraWhitespace ctermbg=red guibg=red
autocmd ColorScheme * highlight ExtraWhitespace ctermbg=red guibg=red

" Status
set laststatus=2
set statusline=
set statusline+=%<\                       " cut at start
set statusline+=%2*[%n%H%M%R%W]%*\        " buffer number, and flags
set statusline+=%-40f\                    " relative path
set statusline+=%=                        " seperate between right- and left-aligned
set statusline+=%1*%y%*%*\                " file type
set statusline+=%10(L(%l/%L)%)\           " line
set statusline+=%2(C(%v/125)%)\           " column
set statusline+=%P                        " percentage of file

" ========= Plugin Options ========

let g:AckAllFiles = 0
let g:AckCmd = 'ack --type-add ruby=.feature --ignore-dir=tmp 2> /dev/null'

let html_use_css=1
let html_number_lines=0
let html_no_pre=1

let vimclojure#WantNailgun = 0
let vimclojure#HighlightBuiltins = 1
let vimclojure#ParenRainbow = 1

let g:gist_clip_command = 'pbcopy'
let g:gist_detect_filetype = 1

let g:rubycomplete_buffer_loading = 1


let g:no_html_toolbar = 'yes'

let coffee_no_trailing_space_error = 1

let NERDTreeIgnore=['\.pyc', '\.o', '\.class', '\.lo']
let NERDTreeHijackNetrw = 0
let NERDTreeShowHidden=1

let g:netrw_banner = 0

let g:ctrlp_match_window = 'top,order:ttb,min:1,max:15,results:15'
let g:ctrlp_prompt_mappings = {
  \ 'PrtSelectMove("j")':   ['<c-n>', '<down>'],
  \ 'PrtSelectMove("k")':   ['<c-p>','<c-k>', '<up>'],
  \ 'PrtHistory(-1)':       ['<c-j>'],
  \ 'PrtHistory(1)':        ['<c-k>'],
  \ }

" ========= Shortcuts ========

" NERDTree
noremap <silent> <LocalLeader>nt :NERDTreeToggle<CR>
noremap <silent> <LocalLeader>nr :NERDTree<CR>
noremap <silent> <LocalLeader>nf :NERDTreeFind<CR>

" CtrlP
noremap <silent> <leader>ff :CtrlP<CR>
noremap <silent> <leader>fr :CtrlPClearCache<CR>
noremap <silent> <leader>fb :CtrlPBuffer<CR>

" Ack
noremap <LocalLeader>aw :Ack '<C-R><C-W>'

" TComment
noremap <silent> <LocalLeader>cc :TComment<CR>
noremap <silent> <LocalLeader>uc :TComment<CR>

" Vimux
let g:VimuxUseNearestPane = 1
noremap <silent> <LocalLeader>rl :wa<CR> :VimuxRunLastCommand<CR>
noremap <silent> <LocalLeader>vi :wa<CR> :VimuxInspectRunner<CR>
noremap <silent> <LocalLeader>vk :wa<CR> :VimuxInterruptRunner<CR>
noremap <silent> <LocalLeader>vx :wa<CR> :VimuxClosePanes<CR>
noremap <silent> <LocalLeader>vp :VimuxPromptCommand<CR>
vnoremap <silent> <LocalLeader>vs "vy :call VimuxRunCommand(@v)<CR>
nnoremap <silent> <LocalLeader>vs vip<LocalLeader>vs<CR>

autocmd FileType coffee noremap <silent> <LocalLeader>rb :call VimuxRunCommand("clear; mocha " . bufname("%"))<CR>
autocmd FileType javascript noremap <silent> <LocalLeader>rb :call VimuxRunCommand("clear; mocha " . bufname("%"))<CR>

noremap <silent> <LocalLeader>rt :!ctags -R --exclude=".git\|.svn\|log\|tmp\|db\|pkg" --extra=+f --langmap=Lisp:+.clj<CR>

noremap <silent> <LocalLeader>cj :!clj %<CR>

noremap <silent> <LocalLeader>gd :e product_diff.diff<CR>:%!git diff<CR>:setlocal buftype=nowrite<CR>
noremap <silent> <LocalLeader>pd :e product_diff.diff<CR>:%!svn diff<CR>:setlocal buftype=nowrite<CR>

noremap <silent> <LocalLeader>nh :nohls<CR>

noremap <silent> <LocalLeader>bd :bufdo :bd<CR>

cnoremap <Tab> <C-L><C-D>

nnoremap <silent> k gk
nnoremap <silent> j gj
nnoremap <silent> Y y$

noremap <silent> <LocalLeader>ws :highlight clear ExtraWhitespace<CR>

" Pasting over a selection does not replace the clipboard
xnoremap <expr> p 'pgv"'.v:register.'y'

" ========= Insert Shortcuts ========

inoremap <C-L> <SPACE>=><SPACE>

" ========= Functions ========

command! SudoW w !sudo tee %

" http://techspeak.plainlystated.com/2009/08/vim-tohtml-customization.html
function! DivHtml(line1, line2)
  exec a:line1.','.a:line2.'TOhtml'
  %g/<style/normal $dgg
  %s/<\/style>\n<\/head>\n//
  %s/body {/.vim_block {/
  %s/<body\(.*\)>\n/<div class="vim_block"\1>/
  %s/<\/body>\n<\/html>/<\/div>
  "%s/\n/<br \/>\r/g

  set nonu
endfunction
command! -range=% DivHtml :call DivHtml(<line1>,<line2>)

function! GitGrepWord()
  cgetexpr system("git grep -n '" . expand("<cword>") . "'")
  cwin
  echo 'Number of matches: ' . len(getqflist())
endfunction
command! -nargs=0 GitGrepWord :call GitGrepWord()
nnoremap <silent> <Leader>gw :GitGrepWord<CR>

function! Trim()
  %s/\s*$//
endfunction
command! -nargs=0 Trim :call Trim()
nnoremap <silent> <Leader>cw :Trim<CR>

function! __HardMode()
  nnoremap h <nop>
  nnoremap j <nop>
  nnoremap k <nop>
  nnoremap l <nop>
endfunction

"-------- Local Overrides
"If you have options you'd like to override locally for
"some reason (don't want to store something in a
"publicly-accessible repository, machine-specific settings, etc.),
"you can create a '.local_vimrc' file in your home directory
"(ie: ~/.vimrc_local) and it will be 'sourced' here and override
"any settings in this file.

"NOTE: YOU MAY NOT WANT TO ADD ANY LINES BELOW THIS
if filereadable(expand('~/.vimrc_local'))
  source ~/.vimrc_local
end
